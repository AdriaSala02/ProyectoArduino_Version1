// ESTACIÓN TIERRA
// Python <===> Estación de Tierra

#include <SoftwareSerial.h>

#define LED_PIN     13   // LED de actividad (verde)
#define LED_ERROR    12   // LED rojo de error 
#define BAUDRATE  9600

SoftwareSerial mySerial(10, 11);  // RX, TX con Satélite


// Funciones para el checksum

uint8_t calcularChecksum(const String &s) {
  uint8_t sum = 0;
  for (unsigned int i = 0; i < s.length(); i++) {
    sum += (uint8_t)s[i];
  }
  return sum;
}

bool verificarYQuitarChecksum(String &frame) {
  int pos = frame.indexOf('*');

  if (pos < 0) {
    // No hay checksum -> aceptamos sin verificar
    return true;
  }

  String data  = frame.substring(0, pos);
  String csStr = frame.substring(pos + 1);
  csStr.trim();
  if (csStr.length() == 0) return false;

  int csRecv = csStr.toInt();  // checksum recibido en DECIMAL
  if (csRecv < 0 || csRecv > 255) return false;

  uint8_t csCalc = calcularChecksum(data);
  if (csCalc != (uint8_t)csRecv) {
    return false;
  }

  frame = data;   // nos quedamos solo con "16:...:...:..."
  return true;
}


void setup() {

  Serial.begin(BAUDRATE);
  mySerial.begin(BAUDRATE);

  pinMode(LED_PIN, OUTPUT);
  pinMode(LED_ERROR, OUTPUT);
  digitalWrite(LED_PIN, LOW);
  digitalWrite(LED_ERROR, LOW);

  Serial.setTimeout(50);
  mySerial.setTimeout(50);

  Serial.println("Estación de tierra lista. Esperando comandos desde Python.");
}

void loop() {

// ================================
  // Satélite <--> Estación tierra
// ================================
  String com ="";    // se declara como nulo para que exista tmb fuera del bucle
  String com_sat = "";
  while (mySerial.available()) {

    // Lee y envia de sat --> Python
    com_sat = mySerial.readStringUntil('|');   // toda la comuniación pasa por "com_sat" para leerse o enviar
    
    if (com_sat.length() > 0) {

      if (!verificarYQuitarChecksum(com_sat)) {
        // Checksum incorrecto: encendemos LED_ERROR y avisamos a Python
        digitalWrite(LED_ERROR, HIGH);
        Serial.print("CHKERR:");        // hay que corregir este error para que se implemente en el protocolo
        Serial.print(com_sat);
        Serial.print('|');
        continue;  // NO reenviamos la trama corrupta
      }

      Serial.print(com_sat);
      Serial.print('|'); // se elimina al leer

  
    // aqui es donde pondremos todo lo que tiene que hacer dependiendo de lo que envie el satelite
    // por ejemplo, si envia errores de datos

    }
    if (com_sat.equals("16:1:-1") || com_sat.equals("16:1:-2")) { // error lectura del DHS || error ángulo
        digitalWrite(LED_ERROR, HIGH);
      } else {
        // se apaga si no hay error
        digitalWrite(LED_ERROR, LOW);
      }
    }
  

// ==================================
  // Estación tierra <--> Python
// ==================================
  while (Serial.available()) {
    com = Serial.readStringUntil('|');
  }

  if (com.length() == 0) {            // Hay Error?
      return;
  }

// =================================================================
  
  //mensajes de texto de python al sat
  if (com.startsWith("16:0")) {
    mySerial.print(com);
    mySerial.print('|');
  }

//SENSOR DE HUMEDAD Y DISTANCIA
  if (com.startsWith("16:1:")) {
    mySerial.print(com);
    mySerial.print('|');

    // Aquí solo van las funciones que modifican algo en tierra
    // El resto se mandan directamente al satélite

    if (com.equals("16:1:1")) {        // INICIO
      digitalWrite(LED_PIN,HIGH);
    }

    else if (com.equals("16:1:2")) {   // 16:1:2| PARAR
      digitalWrite(LED_PIN, LOW);
    }

    else if (com.equals("16:1:3")) {   // 16:1:3| REANUDAR
      digitalWrite(LED_PIN, HIGH);

    }
  }

// =================================================================
// SERVO 

  else if (com.startsWith("16:2:")) {
    mySerial.print(com);
    mySerial.print('|');
  }

// =================================================================
// Distancia??
}

